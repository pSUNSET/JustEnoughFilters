plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = file("src/main/resources/justenoughfilters.accesswidener")
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"
    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_api_version+$minecraft_version"
    modImplementation "net.fabricmc:fabric-language-kotlin:$fabric_kotlin_version"

    modImplementation "mezz.jei:jei-$minecraft_version-fabric:$jei_version"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"

    modCompileOnly "dev.emi:emi-fabric:$emi_version+$minecraft_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property 'version', "$mod_version"
    inputs.property 'group', "$maven_group"

    filesMatching('fabric.mod.json') {
        expand(
                version: "$mod_version",
                minecraft_version_fabric_range: "$minecraft_version_fabric_range",
                fabric_loader_version_range: "$fabric_loader_version_range",
                fabric_api_version_range: "$fabric_api_version_range",
                fabric_kotlin_version_range: "$fabric_kotlin_version_range"
        )
    }
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : "$mod_id",
                'Specification-Vendor'    : "Project-SUNSET",
                'Specification-Version'   : "1",
                'Implementation-Title'    : "$archiveBaseName",
                'Implementation-Version'  : this.project.jar.archiveVersion,
                'Implementation-Vendor'   : "Project-SUNSET",
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
