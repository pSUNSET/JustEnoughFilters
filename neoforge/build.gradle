plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = "NeoForged"
        url = "https://maven.neoforged.net/releases"
    }
    maven {
        name = "Kotlin for Forge"
        url = "https://thedarkcolour.github.io/KotlinForForge/"
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$neoforge_version"
    implementation "thedarkcolour:kotlinforforge-neoforge:$kff_version"

    modCompileOnly "mezz.jei:jei-$minecraft_version-neoforge:$jei_version"

    modImplementation "me.shedaniel:RoughlyEnoughItems-neoforge:$rei_version"

//    modCompileOnly "dev.emi:emi-neoforge:$emi_version+$minecraft_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', "$mod_version"

    filesMatching('META-INF/neoforge.mods.toml') {
        expand([
                version: "$mod_version",
                neoforge_version_range: "$neoforge_version_range",
                minecraft_version_neoforge_range: "$minecraft_version_neoforge_range",
                kff_version_range: "$kff_version_range"
        ])
    }
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : "$mod_id",
                'Specification-Vendor'    : "Project-SUNSET",
                'Specification-Version'   : "1",
                'Implementation-Title'    : "$archiveBaseName",
                'Implementation-Version'  : this.project.jar.archiveVersion,
                'Implementation-Vendor'   : "Project-SUNSET",
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
