plugins {
    id 'com.gradleup.shadow'
}

loom {
    forge {
        mixinConfig "justenoughfilters.mixins.json"
    }
}


architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = "Forge"
        url = "https://files.minecraftforge.net/maven/"
    }
    maven {
        name = "Kotlin for Forge"
        url = "https://thedarkcolour.github.io/KotlinForForge/"
    }
}

dependencies {
    forge "net.minecraftforge:forge:$minecraft_version-$forge_version"
    implementation "thedarkcolour:kotlinforforge:$kff_version"

    modCompileOnly "mezz.jei:jei-$minecraft_version-forge:$jei_version"

    modImplementation "me.shedaniel:RoughlyEnoughItems-forge:$rei_version"

    modCompileOnly "dev.emi:emi-forge:$emi_version+$minecraft_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
}

processResources {
    inputs.property 'version', "$mod_version"

    filesMatching('META-INF/mods.toml') {
        expand([
                version: "$mod_version",
                forge_version_range: "$forge_version_range",
                minecraft_version_forge_range: "$minecraft_version_forge_range",
                kff_version_range: "$kff_version_range"
        ])
    }
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : "$mod_id",
                'Specification-Vendor'    : "Project-SUNSET",
                'Specification-Version'   : "1",
                'Implementation-Title'    : "$archiveBaseName",
                'Implementation-Version'  : this.project.jar.archiveVersion,
                'Implementation-Vendor'   : "Project-SUNSET",
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
